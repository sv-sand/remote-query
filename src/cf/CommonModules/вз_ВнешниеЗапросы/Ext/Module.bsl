///////////////////////////////////////////////////////////////////////////////////////////////////////
// Внешние запросы.
// Светлаков А. В., 09.02.2023
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает текущую подсистемы "Внешние запросы"
// 
// Возвращаемое значение:
//  Строка - версия протокола
//
Функция ВерсияПодсистемы() Экспорт
	
	Возврат "1.0.0.1";
		
КонецФункции

// Возвращает текущую протокола "Внешние запросы"
// 
// Возвращаемое значение:
//  Строка - версия протокола
//
Функция ВерсияПротокола() Экспорт
	
	Возврат "0.3";
		
КонецФункции

// Возвращает список версий поддерживаемых в текущей версии подсистемы
// 
// Возвращаемое значение:
//  Массив - Список версий
//
Функция ПоддерживаемыеВерсииПротокола() Экспорт

	Результат = Новый Массив;
	
	Результат.Добавить("0.2");
	Результат.Добавить("0.2.1");
	Результат.Добавить("0.3");
	
	Возврат Результат;
	
КонецФункции

#Область Данные

// Возвращает ссылку на объект
//
// Параметры:
//  Менеджер	 - ОбъектМенеджер	 - Менеджер для получения ссылки, например "Справочник.Номенклатура"
//  СтрокаУИД	 - Строка			 - Уникальный идентификатор
// 
// Возвращаемое значение:
//   ЛюбаяСсылка - Ссылка на объект базы
//
Функция ПолучитьСсылку(Менеджер, СтрокаУИД) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтрокаУИД) Тогда
		Значение = Неопределено;
	КонецЕсли;
	
	УИД = Новый УникальныйИдентификатор(СтрокаУИД);
	Значение = Менеджер.ПолучитьСсылку(УИД);
	Ссылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение, "Ссылка");
	Если НЕ ЗначениеЗаполнено(Значение)
		ИЛИ НЕ ЗначениеЗаполнено(Ссылка)
		Тогда
		Значение = Неопределено;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Возвращает ссылку на объект, реквизиты которой совпадают с указанными
//
// Параметры:
//  Менеджер	 - Строка - Менеджер объекта для получения ссылки, например "Справочник.Номенклатура"
//  Реквизиты	 - Структура, Соответствие	 - Структура или соответствие, ключ которого - имя реквизита объекта,
//												а значение - это значение реквизита
// 
// Возвращаемое значение:
//  ЛюбаяСсылка - Искомая ссылка
//
Функция ПолучитьСсылкуПоРеквизитам(Менеджер, Реквизиты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Менеджер.Ссылка КАК Ссылка
		|ИЗ
		|	&Менеджер КАК Менеджер
		|ГДЕ 
		|	ИСТИНА";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Менеджер", Менеджер);
	
	Для каждого Реквизит Из Реквизиты Цикл
		Запрос.УстановитьПараметр(Реквизит.Ключ, Реквизит.Значение);		
		Запрос.Текст = Запрос.Текст + Символы.ПС + СтрШаблон("	И Менеджер.%1 = &%1", Реквизит.Ключ);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
		
КонецФункции

// Возвращает кеш ссылок по Уникальным идентификаторам
//
// Параметры:
//  ИмяМетаданных	 - Строка	 - Имя объекта метаданных, например "Справочник.Контрагенты"
//  МассивСтрокУИД	 - Массив	 - Массив строк уникальных идентификаторов
// 
// Возвращаемое значение:
//  Соответвтвие - Соответвтсвие ключи которого - строки уникальных идентификаторов, 
//					а значения - ссылки на объекты базы данных
//
Функция ПолучитьКешСсылок(ИмяМетаданных, МассивСтрокУИД) Экспорт
	
	Результат = Новый Соответствие;
	
	МассивУИД = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСтрокУИД);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УникальныйИдентификатор(Объект.Ссылка) КАК УИД,
		|	Объект.Ссылка КАК Ссылка
		|ИЗ 
		|	&ИмяМетаданных КАК Объект
		|ГДЕ
		|	УникальныйИдентификатор(Объект.Ссылка) В (&МассивУИД)";
	
	Запрос.УстановитьПараметр("МассивУИД", МассивУИД);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяМетаданных", ИмяМетаданных);
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.УИД, Выборка.Ссылка);		
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти

#КонецОбласти
