
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Возвращает элемент справочника настройки подключения
//
// Параметры:
//  Идентификатор	 - Строка	 - Идентификатор настроек
// 
// Возвращаемое значение:
//   - Ссылка - Элемент справочника настройки подключения
//
Функция ПолучитьНастройкиПодключения(Идентификатор) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Настройки.Ссылка КАК Ссылка,
		|	Настройки.Идентификатор КАК Идентификатор,
		|	Настройки.ВидПодключения КАК ВидПодключения,
		|	Настройки.СтрокаПодключения КАК СтрокаПодключения,
		|	Настройки.ИмяПользователя КАК ИмяПользователя,
		|	Настройки.ВерсияПротокола КАК ВерсияПротокола
		|ИЗ
		|	Справочник.вз_НастройкиПодключения КАК Настройки
		|ГДЕ
		|	Настройки.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Текст = СтрШаблон("Не удалось найти настройки подключения по идентификатору %1", Идентификатор);
		вз_ВнешниеЗапросыСлужебный.ЗаписатьВЖурнал(Текст, УровеньЖурналаРегистрации.Ошибка, "Настройки");
		ВызватьИсключение Текст;	
	КонецЕсли;
	
	Настройки = ОбменДаннымиСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли