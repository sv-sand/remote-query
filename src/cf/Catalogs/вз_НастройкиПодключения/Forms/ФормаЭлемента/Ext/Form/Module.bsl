
#Область Команды

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	Если Модифицированность 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка)
		Тогда
		ПоказатьПредупреждение(, "Требуется записать настройки", , "Внимание!");
		Возврат;
	КонецЕсли;
	
	Результат = ПроверитьПодключениеНаСервере();
	
	ПоказатьПредупреждение(, Результат, 10, "Проверка подключения");
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПодключениеНаСервере()
	
	ПараметрыПодключения = вз_ВнешниеЗапросыИсходящие.ПолучитьПараметрыПодключения(Объект.Идентификатор);
	Результат = вз_ВнешниеЗапросыИсходящие.ПроверкаПодключения(ПараметрыПодключения);	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Служебные

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	Если Объект.ВидПодключения = 
		ПредопределенноеЗначение("Перечисление.вз_ВидыПодключенияКБазамДанных.ВнешнийЗапрос") 
		Тогда
		Элементы.СтрокаПодключения.Подсказка = 
			"Адрес публикации http-сервиса, в формате ""http://server-name/base-name/hs""";
	Иначе
		Элементы.СтрокаПодключения.Подсказка = "Строка подключения";
	КонецЕсли;
	
	Элементы.ВерсияПротокола.Видимость = 
		Объект.ВидПодключения = ПредопределенноеЗначение("Перечисление.вз_ВидыПодключенияКБазамДанных.ВнешнийЗапрос");
	Элементы.ВерсияПротокола.СписокВыбора.ЗагрузитьЗначения(ПоддерживаемыеВерсииПротокола());
	
КонецПроцедуры

&НаСервере
Функция ПоддерживаемыеВерсииПротокола()
	
	Если Объект.ВидПодключения = 
		ПредопределенноеЗначение("Перечисление.вз_ВидыПодключенияКБазамДанных.ВнешнийЗапрос") 
		Тогда
		Результат = вз_ВнешниеЗапросы.ПоддерживаемыеВерсииПротокола();	
	Иначе
		Результат = Новый Массив;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ЭтоНовый() Тогда
		СсылкаНаОбъект = Справочники.вз_НастройкиПодключения.ПолучитьСсылку(Новый УникальныйИдентификатор());
		ТекущийОбъект.УстановитьСсылкуНового(СсылкаНаОбъект);		
	Иначе
		СсылкаНаОбъект = ТекущийОбъект.Ссылка;
	КонецЕсли;
	
	вз_ВнешниеЗапросыСлужебный.СохранитьПароль(СсылкаНаОбъект, Пароль);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодключенияПриИзменении(Элемент)
	
	Объект.ВерсияПротокола = "";
	
	УправлениеЭлементамиФормы();
		
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
