
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ПроверитьУникальностьИдентификатора(Отказ);
	
	СкорректироватьРеквизитыПоВиду();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУникальностьИдентификатора(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВнешниеЗапросы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.вз_ВнешниеЗапросы КАК ВнешниеЗапросы
		|ГДЕ
		|	ВнешниеЗапросы.Ссылка <> &Ссылка
		|	И ВнешниеЗапросы.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ТекстСообщения = СтрШаблон(
		"ru='Идентификатор %1 уже используется в настройке %2'",
		Наименование,
		Выборка.Ссылка);
		
	ОбщегоНазначения.СообщитьПользователю(НСтр(ТекстСообщения), Выборка.Ссылка, , , Отказ);
		
КонецПроцедуры

Процедура СкорректироватьРеквизитыПоВиду()

	Если Вид = Перечисления.вз_ВидыВнешнихЗапросов.Запрос Тогда
		ИсполняемыйКод = "";
		
	ИначеЕсли Вид = Перечисления.вз_ВидыВнешнихЗапросов.Код Тогда
		ТекстЗапроса = "";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
